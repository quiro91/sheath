import org.gradle.util.VersionNumber

buildscript {
  apply from: rootProject.file('../dependencies.gradle')

  repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
    jcenter()
    maven {
      url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
      url "http://dl.bintray.com/kotlin/kotlin-dev"
    }
  }

  dependencies {
    classpath deps.kotlin.gradle_plugin
    classpath deps.maven_publishing_plugin
    classpath deps.gradle_publishing_plugin
    classpath deps.ktlint_plugin
  }
}

repositories {
  google()
  mavenCentral()
  jcenter()
  maven {
    url "https://dl.bintray.com/kotlin/kotlin-eap"
  }
  maven {
    url "http://dl.bintray.com/kotlin/kotlin-dev"
  }
}

apply from: rootProject.file('copy_properties.gradle')

apply plugin: 'java-gradle-plugin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

apply from: rootProject.file('generate_build_properties.gradle')

gradlePlugin {
  plugins {
    sheathPlugin {
      id = "dev.quiro.sheath"
      displayName = project.findProperty('POM_NAME')
      implementationClass = "dev.quiro.sheath.plugin.SheathPlugin"
      description = project.findProperty('POM_DESCRIPTION')
    }
  }
}

pluginBundle {
  website = project.findProperty('POM_URL')
  vcsUrl = project.findProperty('POM_SCM_URL')
  tags = ['dagger2', 'dagger2-android', 'kotlin', 'kotlin-compiler-plugin']
}

tasks.withType(JavaCompile).configureEach {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

//noinspection UnnecessaryQualifiedReference
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8

    // Because Gradle hardcodes Kotlin versions we have warnings during compilation.
    allWarningsAsErrors = rootProject.ext.ci &&
        VersionNumber.parse(rootProject.ext.kotlinVersion).baseVersion < VersionNumber.parse("1.5.0").baseVersion
  }
}

dependencies {
  implementation deps.kotlin.gradle_plugin
  implementation deps.kotlin.gradle_plugin_api

  // Necessary to bump a transitive dependency.
  compileOnly deps.kotlin.reflect

  // Compile only so that Java / Kotlin modules don't download it unnecessarily.
  compileOnly deps.android_gradle_plugin
}
